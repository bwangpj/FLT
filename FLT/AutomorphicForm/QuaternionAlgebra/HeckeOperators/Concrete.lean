/-
Copyright (c) 2025 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Andrew Yang, Matthew Jasper
-/
import FLT.AutomorphicForm.QuaternionAlgebra.HeckeOperators.Abstract -- abstract Hecke ops
import FLT.AutomorphicForm.QuaternionAlgebra.Defs -- definitions of automorphic forms
import FLT.QuaternionAlgebra.NumberField -- rigidifications of quat algs
import Mathlib.NumberTheory.NumberField.InfinitePlace.TotallyRealComplex
import Mathlib.RingTheory.DedekindDomain.FiniteAdeleRing
import FLT.DedekindDomain.FiniteAdeleRing.LocalUnits -- for (Ï€ 0; 0 1)
import FLT.Mathlib.Topology.Algebra.RestrictedProduct
/-

# Concrete Hecke operators

Hecke operators for spaces of automorphic forms on totally definite quaternion algebras
of level `Uâ‚(S)`, where `S` is a finite set of finite places of the totally real number
field `F`, and `Uâ‚(S)` is the matrices which are of the form `(a *;0 a)` mod `v` for
all `v âˆˆ S`.

## Main definitions

All in the `TotallyDefiniteQuaternionAlgebra.WeightTwoAutomorphicForm` namespace.

Let `r : Rigidification F D` be an `ğ”¸_F^âˆ`-algebra isomorphism `D âŠ—[F] ğ”¸_F^âˆ = Mâ‚‚(ğ”¸_F^âˆ)`,
needed to interpret the local factors `Dáµ¥` as matrix rings so we can define Hecke operators
as matrices.

* `HeckeOperator.T r R v` -- the Hecke operator `Táµ¥` associated to `(Ï–áµ¥ 0; 0 1)` at a (good)
  place `v` (via the rigidification `r`), as an `R`-linear endomorphism of
  `WeightTwoAutomorphicFormOfLevel (U1 r S) R`.
* `HeckeOperator.U r S R a ha` -- the Hecke operator `Uáµ¥,â‚` associated to `(a 0;0 1)` at
  a (bad) place `v`; here `a : v.adicCompletionIntegers F` and `ha` is a proof that `a â‰  0`,
  as an `R`-linear endomorphism of `WeightTwoAutomorphicFormOfLevel (U1 r S) R`.
* `HeckeAlgebra F D r S R` -- the Hecke algebra generated by the `Táµ¥` and `Uáµ¥,â‚`. Built
  as a type, coerced from a subalgebra of the endomorphism algebra of the forms.
* `HeckeAlgebra.T D r R v hv` -- `Táµ¥` as a term of type `HeckeAlgebra F D r S R`
* `HeckeAlgebra.U D r R v hv a ha` -- `Uáµ¥,â‚` as a term of type `HeckeAlgebra F D r S R`

-/

/-

## A finiteness result

The existence of abstract Hecke operators relies on a certain double coset space being
a finite union of single cosets. In our situation we can supply this finiteness proof
via a topological argument, which we abstract here.

-/

section finiteness

open Topology Set

variable {G : Type*} [Group G] [TopologicalSpace G] [IsTopologicalGroup G]
    {g : G} {U V : Subgroup G}

open scoped Pointwise in
lemma QuotientGroup.mk_image_finite_of_compact_of_open (hU : IsCompact (U : Set G))
    (hV : IsCompact (V : Set G)) (hVopen : IsOpen (V : Set G)) :
    (QuotientGroup.mk '' (U * g â€¢ V) : Set (G â§¸ V)).Finite := by
  have : DiscreteTopology (G â§¸ V) := by
    rw [â† forall_open_iff_discrete]
    intro s
    rw [â† (isQuotientMap_mk V).isOpen_preimage, â† (QuotientGroup.mk_surjective).image_preimage s,
      preimage_image_mk_eq_iUnion_image, iUnion_subtype]
    conv in â‹ƒ x âˆˆ _, _ => change â‹ƒ x âˆˆ (V : Set G), _
    rw [iUnion_mul_right_image]
    exact IsOpen.mul_left hVopen
  exact ((hU.mul <| hV.image (by fun_prop)).image continuous_mk).finite_of_discrete

end finiteness


open NumberField IsQuaternionAlgebra.NumberField IsDedekindDomain

-- let F be a totally real number field
variable (F : Type*) [Field F] [NumberField F] [IsTotallyReal F]

-- Let D/F be a quaternion algebra
variable (D : Type*) [Ring D] [Algebra F D] [IsQuaternionAlgebra F D]

-- Let r be a rigidification of D, which is a collection of isomorphisms D âŠ— Fáµ¥ = Mâ‚‚(Fáµ¥)
-- for all finite places v of F, compatible with the adelic structure (i.e. inducing
-- an isomorphism D âŠ—_F ğ”¸_F^f = Mâ‚‚(ğ”¸_F^f))
variable (r : Rigidification F D)

-- Let S be a finite set of finite places of F (the level)
variable (S : Finset (HeightOneSpectrum (ğ“ F)))

-- let P be a good prime
variable {P : HeightOneSpectrum (ğ“ F)} (hP : P âˆ‰ S)

open TotallyDefiniteQuaternionAlgebra
-- let's do T_P : S_2^D(U_1(S)) -> S_2^D(U_1(S))
namespace TotallyDefiniteQuaternionAlgebra.WeightTwoAutomorphicForm

open IsDedekindDomain.HeightOneSpectrum

open scoped TensorProduct

variable {F D} in
open scoped TensorProduct.RightActions in
/-- U1(S) -/
noncomputable abbrev U1 : Subgroup (D âŠ—[F] (IsDedekindDomain.FiniteAdeleRing (ğ“ F) F))Ë£ :=
  Subgroup.map (Units.map r.symm.toMonoidHom) (GL2.TameLevel S)

variable {F D} in
open scoped TensorProduct.RightActions in
lemma U1_compact : IsCompact (U1 r S : Set (D âŠ—[F] (IsDedekindDomain.FiniteAdeleRing (ğ“ F) F))Ë£) :=
  sorry -- #583, long

variable {F D} in
open scoped TensorProduct.RightActions in
lemma U1_open : IsOpen (U1 r S : Set (D âŠ—[F] (IsDedekindDomain.FiniteAdeleRing (ğ“ F) F))Ë£) :=
  sorry -- #583, long

variable (R : Type*) [CommRing R]

namespace HeckeOperator

variable {F D S} in
set_option maxSynthPendingDepth 1 in
open scoped TensorProduct.RightActions in
/-- The Hecke operator T_v as an R-linear map from R-valued quaternionic weight 2
automorphic forms of level U_1(S).
-/
noncomputable def T (v : HeightOneSpectrum (ğ“ F)) :
    WeightTwoAutomorphicFormOfLevel (U1 r S) R â†’â‚—[R]
    WeightTwoAutomorphicFormOfLevel (U1 r S) R :=
  letI : DecidableEq (HeightOneSpectrum (ğ“ F)) := Classical.typeDecidableEq _
  let g : (D âŠ—[F] (IsDedekindDomain.FiniteAdeleRing (ğ“ F) F))Ë£ :=
    Units.map r.symm.toMonoidHom (Matrix.GeneralLinearGroup.diagonal
    ![FiniteAdeleRing.localUniformiserUnit F v, 1])
  AbstractHeckeOperator.HeckeOperator (R := R) g (U1 r S) (U1 r S)
  (QuotientGroup.mk_image_finite_of_compact_of_open (U1_compact r S) (U1_compact r S) (U1_open r S))

variable {F D} in
set_option maxSynthPendingDepth 1 in
open scoped TensorProduct.RightActions in
/-- The Hecke operator U_{v,Î±} associated to the matrix (Î± 0;0 1) at v,
considered as an R-linear map from R-valued quaternionic weight 2
automorphic forms of level U_1(S). Here Î± is a nonzero element of ğ“áµ¥.
We do not demand the condition v âˆˆ S, the bad primes, but this operator
should only be used in this setting. See also `T r v` for the good primes.
-/
noncomputable def U {v : HeightOneSpectrum (ğ“ F)}
    (Î± : v.adicCompletionIntegers F) (hÎ± : Î± â‰  0) :
    WeightTwoAutomorphicFormOfLevel (U1 r S) R â†’â‚—[R]
    WeightTwoAutomorphicFormOfLevel (U1 r S) R :=
  letI : DecidableEq (HeightOneSpectrum (ğ“ F)) := Classical.typeDecidableEq _
  let g : (D âŠ—[F] (IsDedekindDomain.FiniteAdeleRing (ğ“ F) F))Ë£ :=
    Units.map r.symm.toMonoidHom (Matrix.GeneralLinearGroup.diagonal
    ![FiniteAdeleRing.localUnit F âŸ¨(Î± : v.adicCompletion F),
    (Î± : v.adicCompletion F)â»Â¹, by
      rw [mul_inv_cancelâ‚€]
      exact_mod_cast hÎ±, by
      rw [inv_mul_cancelâ‚€]
      exact_mod_cast hÎ±âŸ©, 1])
  AbstractHeckeOperator.HeckeOperator (R := R) g (U1 r S) (U1 r S)
  (QuotientGroup.mk_image_finite_of_compact_of_open (U1_compact r S) (U1_compact r S) (U1_open r S))

lemma _root_.Ne.mul {Mâ‚€ : Type*} [Mul Mâ‚€] [Zero Mâ‚€] [NoZeroDivisors Mâ‚€] {a b : Mâ‚€}
  (ha : a â‰  0) (hb : b â‰  0) : a * b â‰  0 := mul_ne_zero ha hb

open scoped Pointwise

section CosetComputation
-- local F_v

-- set_option synthInstance.maxHeartbeats 0

variable (v : HeightOneSpectrum (ğ“ F))
variable (Î± : v.adicCompletionIntegers F)
variable (hÎ± : Î± â‰  0)

variable {F Î± hÎ±} in
noncomputable def U1v : Subgroup (GL (Fin 2) (adicCompletion F v)) := (GL2.localTameLevel v)

variable {F v} in
noncomputable def g : (GL (Fin 2) (adicCompletion F v)) :=
  Matrix.GeneralLinearGroup.diagonal (![âŸ¨(Î± : v.adicCompletion F),
    (Î± : v.adicCompletion F)â»Â¹, by
      rw [mul_inv_cancelâ‚€]
      exact_mod_cast hÎ±, by
      rw [inv_mul_cancelâ‚€]
      exact_mod_cast hÎ±âŸ©, 1])

set_option synthInstance.maxHeartbeats 0 in
-- double coset space
variable {F v} in
noncomputable def doubleCosets :
  Set ((GL (Fin 2) (adicCompletion F v)) â§¸ â†‘(U1v v)) :=
  (QuotientGroup.mk '' ((U1v v) * g Î± hÎ± â€¢ â†‘(U1v v) ))

variable {F v} in
noncomputable def gt (t : v.adicCompletionIntegers F) : (GL (Fin 2) (adicCompletion F v)) := by
  let gtInv : Invertible !![(Î± : v.adicCompletion F), t; 0, 1].det :=
  { invOf := (Î± : v.adicCompletion F)â»Â¹,
    invOf_mul_self :=
      by simp only [Matrix.det_fin_two_of,
        mul_one, mul_zero, sub_zero]; rw [inv_mul_cancelâ‚€]; exact_mod_cast hÎ±,
    mul_invOf_self :=
      by simp only [Matrix.det_fin_two_of,
        mul_one, mul_zero, sub_zero]; rw [mul_inv_cancelâ‚€]; exact_mod_cast hÎ± }
  exact Matrix.unitOfDetInvertible !![(Î± : v.adicCompletion F), t; 0, 1]

variable {F v Î± hÎ±} in
noncomputable def ht (t : v.adicCompletion F) : (GL (Fin 2) (adicCompletion F v)) := by
  let htInv : Invertible !![1, t; 0, 1].det :=
  { invOf := 1,
    invOf_mul_self :=
      by simp only [Matrix.det_fin_two_of, mul_one, mul_zero, sub_zero],
    mul_invOf_self :=
      by simp only [Matrix.det_fin_two_of, mul_one, mul_zero, sub_zero] }
  exact Matrix.unitOfDetInvertible !![1, t; 0, 1]

variable {F v} in
noncomputable def singleCosetsFunction
  (t : â†‘(adicCompletionIntegers F v) â§¸ (AddSubgroup.map (AddMonoidHom.mulLeft Î±)
    (âŠ¤ : AddSubgroup â†‘(adicCompletionIntegers F v)))) :
  ((GL (Fin 2) (adicCompletion F v)) â§¸ â†‘(U1v v)) := by
  let tLift : â†‘(adicCompletionIntegers F v) := Quotient.out t
  exact QuotientGroup.mk (gt Î± hÎ± tLift)

set_option maxHeartbeats 400000
-- explicit matrix coset computations
variable {F v} in
lemma U_coset : Set.BijOn (singleCosetsFunction Î± hÎ±) âŠ¤ (doubleCosets Î± hÎ±) := by
  have r (A : Matrix (Fin 2) (Fin 2) (adicCompletion F v)) [Invertible A.det] :
    (â†‘(A.unitOfDetInvertible) : Matrix (Fin 2) (Fin 2) (adicCompletion F v)) = A := rfl
  constructor
  Â· intro t h
    have m : (gt Î± hÎ± (Quotient.out t)) =  ht â†‘(Quotient.out t) * g Î± hÎ± := by
        have râ‚ : (g Î± hÎ± : Matrix (Fin 2) (Fin 2) (adicCompletion F v))
          = !![â†‘Î±, 0; 0, 1] := by
          rw[g]
          ext i j
          rw[Matrix.GeneralLinearGroup.diagonal]
          fin_cases i
          Â· fin_cases j
            Â· simp
            simp
          fin_cases j
          Â· simp
          simp
        ext i j; push_cast
        rw[gt, ht, râ‚]
        rw[r, r]
        rw[Matrix.mul_apply]
        simp only [Fin.sum_univ_two, Fin.isValue]
        fin_cases i
        Â· fin_cases j
          Â· simp
          simp
        simp
    rw[singleCosetsFunction, m, doubleCosets]
    use (ht â†‘(Quotient.out t) * g Î± hÎ±)
    constructor
    Â· use ht â†‘(Quotient.out t)
      constructor
      Â· rw[ht]
        constructor
        Â· let htInt : ((Matrix (Fin 2) (Fin 2) â†¥(adicCompletionIntegers F v))Ë£) := by
            let htInv : Invertible !![1, (Quotient.out t); 0, 1].det :=
            { invOf := 1,
              invOf_mul_self :=
              by simp only [Matrix.det_fin_two_of, mul_one, mul_zero, sub_zero],
              mul_invOf_self :=
              by simp only [Matrix.det_fin_two_of, mul_one, mul_zero, sub_zero] }
            exact Matrix.unitOfDetInvertible !![1, (Quotient.out t); 0, 1]
          use htInt
          refine Units.eq_iff.mp ?_
          rw[r]
          have ho : (htInt = !![1, (Quotient.out t); 0, 1]) := rfl
          rw[Units.coe_map, ho]
          simp only [RingHom.toMonoidHom_eq_coe, MonoidHom.coe_coe, RingHom.mapMatrix_apply,
            ValuationSubring.coe_subtype]
          ext i j
          fin_cases i
          Â· fin_cases j
            Â· simp
            simp
          fin_cases j
          Â· simp
          simp
        rw[r]
        simp
      use g Î± hÎ±
      simp only [and_true]
      use (1 : GL (Fin 2) (adicCompletion F v))
      simp only [SetLike.mem_coe, smul_eq_mul, mul_one, and_true]
      exact Subgroup.one_mem (U1v v)
    rfl

  constructor
  Â· intro tâ‚ hâ‚ tâ‚‚ hâ‚‚ h
    rw[singleCosetsFunction, singleCosetsFunction] at h
    have hâ‚€ := QuotientGroup.eq.mp h
    have m : (gt Î± hÎ± (Quotient.out tâ‚))â»Â¹ * gt Î± hÎ± (Quotient.out tâ‚‚)
      = ht ((Î± : v.adicCompletion F)â»Â¹ *
        (( - (Quotient.out tâ‚) + (Quotient.out tâ‚‚)) : adicCompletion F v )) := by
        apply inv_mul_eq_iff_eq_mul.mpr
        rw [gt, gt, ht]
        ext i j; push_cast
        rw[r, r, r]
        rw[Matrix.mul_apply]
        simp only [Fin.sum_univ_two, Fin.isValue]
        fin_cases i
        Â· fin_cases j
          Â· simp
          simp only [Fin.zero_eta, Fin.isValue, Fin.mk_one, Matrix.of_apply, Matrix.cons_val',
            Matrix.cons_val_one, Matrix.cons_val_fin_one, Matrix.cons_val_zero, mul_one]
          rw[â† mul_assoc, mul_inv_cancelâ‚€, one_mul]; ring
          have hÎ±â‚ := Subtype.coe_ne_coe.mpr hÎ±; assumption
        simp
    rw[m] at hâ‚€
    obtain âŸ¨ âŸ¨ x, y âŸ© , z âŸ© := hâ‚€
    apply_fun (fun (A : (Matrix (Fin 2) (Fin 2) (adicCompletion F v))Ë£) â†¦ A 0 1) at y
    rw[ht] at y
    simp only [RingHom.toMonoidHom_eq_coe, Fin.isValue, Units.coe_map, MonoidHom.coe_coe,
      RingHom.mapMatrix_apply, ValuationSubring.coe_subtype, Matrix.map_apply] at y
    have w : ((x 0 1) : adicCompletion F v) = (Î± : v.adicCompletion F)â»Â¹ *
        (( - (Quotient.out tâ‚) + (Quotient.out tâ‚‚)) : adicCompletion F v ) := by
        rw[y]; rfl
    conv_lhs =>
      apply Eq.symm (QuotientAddGroup.out_eq' tâ‚)
    conv_rhs =>
      apply Eq.symm (QuotientAddGroup.out_eq' tâ‚‚)
    apply QuotientAddGroup.eq.mpr
    use (x 0 1)
    constructor
    Â· simp
    simp only [Fin.isValue, AddMonoidHom.coe_mulLeft]
    apply (Subtype.coe_inj).mp; push_cast
    rw[w, â† mul_assoc, mul_inv_cancelâ‚€, one_mul]
    have hÎ±â‚ := Subtype.coe_ne_coe.mpr hÎ±; assumption

  intro co h
  obtain âŸ¨ coâ‚€, âŸ¨ âŸ¨ coâ‚, hâ‚, âŸ¨ l, âŸ¨ âŸ¨ coâ‚‚, âŸ¨ hâ‚‚, z âŸ© âŸ© , hl âŸ© âŸ© âŸ© , hâ‚€ âŸ© âŸ© := h
  have hp : coâ‚€ = coâ‚ * (g Î± hÎ±) * coâ‚‚ := by
    rw[â† hl, â† z]; simp only [smul_eq_mul]; rw[mul_assoc]
  obtain âŸ¨ âŸ¨ âŸ¨ val_xâ‚, inv_xâ‚, val_inv_xâ‚, inv_val_xâ‚ âŸ© , y âŸ© , z âŸ© := hâ‚
  let a : (adicCompletionIntegers F v) := (val_xâ‚ 0 0)
  let b : (adicCompletionIntegers F v) := (val_xâ‚ 0 1)
  let c : (adicCompletionIntegers F v) := (val_xâ‚ 1 0)
  let d : (adicCompletionIntegers F v) := (val_xâ‚ 1 1)
  have h11 : c * (inv_xâ‚ 0 1) + d * (inv_xâ‚ 1 1) = 1 := by calc
    _ = (val_xâ‚ 1 0) * (inv_xâ‚ 0 1) + (val_xâ‚ 1 1) * (inv_xâ‚ 1 1) := rfl
    _ = (val_xâ‚ * inv_xâ‚) 1 1 := by rw[Matrix.mul_apply]; simp
    _ = 1 := by rw[val_inv_xâ‚]; simp
  have valc : Valued.v (c : adicCompletion F v) < 1 := by
    have hc : c = (val_xâ‚ 1 0) := rfl
    rw[hc]
    apply_fun (fun (A : (Matrix (Fin 2) (Fin 2) (adicCompletion F v))Ë£) â†¦ A 1 0) at y
    simp only [RingHom.toMonoidHom_eq_coe, Units.map_mk, MonoidHom.coe_coe, RingHom.mapMatrix_apply,
      ValuationSubring.coe_subtype, Fin.isValue, Matrix.map_apply] at y
    rw[y]
    apply z.right
  have maxc : c âˆˆ IsLocalRing.maximalIdeal (adicCompletionIntegers F v) := by
    apply (ValuationSubring.valuation_lt_one_iff (adicCompletionIntegers F v) c).mpr
    have valcâ‚ : Valued.v.IsEquiv (adicCompletionIntegers F v).valuation := by
      apply Valuation.isEquiv_valuation_valuationSubring
    apply (Valuation.isEquiv_iff_val_lt_one.mp valcâ‚).mp
    exact valc
  have maxd : d âˆ‰ IsLocalRing.maximalIdeal (adicCompletionIntegers F v) := by
    by_contra maxdâ‚
    have max1 : c * (inv_xâ‚ 0 1) + d * (inv_xâ‚ 1 1)
      âˆˆ IsLocalRing.maximalIdeal (adicCompletionIntegers F v) := by
      apply Ideal.add_mem
      repeat
        apply Ideal.mul_mem_right
        assumption
    rw[h11] at max1
    have nonunit : 1 âˆˆ nonunits â†¥(adicCompletionIntegers F v) :=
      (IsLocalRing.mem_maximalIdeal 1).mp max1
    exact one_notMem_nonunits nonunit
  have dunit : IsUnit d := by
    by_contra dnotunit
    have dnonunit : d âˆˆ nonunits â†¥(adicCompletionIntegers F v) := mem_nonunits_iff.mpr dnotunit
    have dmax : d âˆˆ IsLocalRing.maximalIdeal (adicCompletionIntegers F v) :=
      (IsLocalRing.mem_maximalIdeal d).mpr dnonunit
    exact maxd dmax
  obtain âŸ¨ dinv, dvalinv, dinvval âŸ© := isUnit_iff_exists.mp dunit

  let t : â†¥(adicCompletionIntegers F v) â§¸ AddSubgroup.map (AddMonoidHom.mulLeft Î±) âŠ¤ := b * dinv
  use t
  simp only [Set.top_eq_univ, Set.mem_univ, true_and]
  rw[singleCosetsFunction, â† hâ‚€]
  apply QuotientGroup.eq.mpr
  rw[hp, â† mul_assoc]
  have uele (uâ‚ : GL (Fin 2) (adicCompletion F v)) (huâ‚ : uâ‚ âˆˆ U1v v)
    (uâ‚‚ : GL (Fin 2) (adicCompletion F v)) (huâ‚‚ : uâ‚‚ âˆˆ U1v v) :
    uâ‚ * uâ‚‚ âˆˆ U1v v := by
    exact (Subgroup.mul_mem_cancel_right (U1v v) huâ‚‚).mpr huâ‚
  have ht : t = b * dinv := rfl
  rw[â† QuotientAddGroup.out_eq' t] at ht
  have htâ‚ := QuotientAddGroup.eq.mp ht
  obtain âŸ¨q, hqâŸ© := htâ‚
  simp only [AddSubgroup.coe_top, Set.mem_univ, AddMonoidHom.coe_mulLeft, true_and] at hq
  have hqâ‚ : Quotient.out t = b * dinv - Î± * q := by rw[hq]; ring
  apply uele
  Â· let muMatrix : Matrix (Fin 2) (Fin 2) (adicCompletion F v) :=
      !![a-(Quotient.out t)*c, (Î± : adicCompletion F v)â»Â¹ * (b-(Quotient.out t)*d); c*Î±, d]
    let mup : GL (Fin 2) (adicCompletion F v) := (gt Î± hÎ± (Quotient.out t))â»Â¹ * (coâ‚ * g Î± hÎ±)
    have hmup : mup = (gt Î± hÎ± (Quotient.out t))â»Â¹ * (coâ‚ * g Î± hÎ±) := rfl
    have m : mup = muMatrix := by
      have hp1 : (gt Î± hÎ± (Quotient.out t))â»Â¹
        = !![(Î± : adicCompletion F v)â»Â¹, -(Î± : adicCompletion F v)â»Â¹*(Quotient.out t);0,1] := by
        rw[gt]
        push_cast; rw[r]
        rw[Matrix.inv_def]
        simp only [Matrix.det_fin_two_of, mul_one, mul_zero, sub_zero, Ring.inverse_eq_inv',
          Matrix.adjugate_fin_two_of, neg_zero, Matrix.smul_of, Matrix.smul_cons, smul_eq_mul,
          mul_neg, Matrix.smul_empty, neg_mul, EmbeddingLike.apply_eq_iff_eq]
        rw [inv_mul_cancelâ‚€]; exact_mod_cast hÎ±
      have hp2 : coâ‚ = !![(a : adicCompletion F v),b;c,d] := by
        rw[â† y]
        ext i j
        simp only [RingHom.toMonoidHom_eq_coe, Units.map_mk, MonoidHom.coe_coe,
          RingHom.mapMatrix_apply, ValuationSubring.coe_subtype, Matrix.map_apply, Matrix.of_apply,
          Matrix.cons_val', Matrix.cons_val_fin_one]
        fin_cases i
        Â· fin_cases j
          Â· simp; rfl
          simp; rfl
        fin_cases j
        Â· simp; rfl
        simp; rfl
      have hp3 : g Î± hÎ± = !![(Î± : adicCompletion F v), 0;0,1] := by
        rw[g]
        ext i j
        simp only [Matrix.of_apply, Matrix.cons_val', Matrix.cons_val_fin_one]
        fin_cases i
        Â· fin_cases j
          Â· simp; rfl
          simp; rfl
        fin_cases j
        Â· simp; rfl
        simp; rfl
      rw[hmup]; push_cast; rw[hp2, hp3]
      norm_cast; rw[hp1]
      unfold muMatrix
      simp only [neg_mul, Matrix.cons_mul, Nat.succ_eq_add_one, Nat.reduceAdd, Matrix.vecMul_cons,
        Matrix.head_cons, Matrix.smul_cons, smul_eq_mul, mul_zero, Matrix.smul_empty,
        Matrix.tail_cons, mul_one, Matrix.empty_vecMul, add_zero, Matrix.add_cons, zero_add,
        Matrix.empty_add_empty, Matrix.empty_mul, Equiv.symm_apply_apply, neg_smul, Matrix.neg_cons,
        Matrix.neg_empty, zero_smul, one_smul, EmbeddingLike.apply_eq_iff_eq]
      ring_nf
      ext i j
      fin_cases i
      Â· fin_cases j
        Â· simp only [Nat.succ_eq_add_one, Nat.reduceAdd, Fin.zero_eta, Fin.isValue,
          Matrix.cons_val', Matrix.cons_val_zero, Matrix.cons_val_fin_one]
          rw [mul_inv_cancelâ‚€]
          Â· simp
          exact_mod_cast hÎ±
        simp
      fin_cases j
      Â· simp
      simp

    rw[â† hmup]

    let muMatrixInt : Matrix (Fin 2) (Fin 2) (adicCompletionIntegers F v) :=
      !![a-(Quotient.out t)*c, q*d; c*Î±, d]
    have inteq : (RingHom.mapMatrix ((v.adicCompletionIntegers F).subtype)).toMonoidHom
      muMatrixInt = muMatrix := by
      simp only [RingHom.toMonoidHom_eq_coe, MonoidHom.coe_coe, RingHom.mapMatrix_apply,
        ValuationSubring.coe_subtype]
      unfold muMatrixInt muMatrix
      ext i j
      fin_cases i
      Â· fin_cases j
        Â· simp
        simp only [Fin.zero_eta, Fin.isValue, Fin.mk_one, Matrix.map_apply, Matrix.of_apply,
          Matrix.cons_val', Matrix.cons_val_one, Matrix.cons_val_fin_one, Matrix.cons_val_zero,
          MulMemClass.coe_mul]
        rw[hqâ‚]
        ring_nf
        sorry -- algebra
      fin_cases j
      Â· simp
      simp
    rw[â† m] at inteq

    constructor
    Â· -- image of unit
      sorry
    -- in localTameLevel
    sorry
  assumption


end CosetComputation

lemma U_mul {v : HeightOneSpectrum (ğ“ F)}
    {Î± Î² : v.adicCompletionIntegers F} (hÎ± : Î± â‰  0) (hÎ² : Î² â‰  0) :
    (U r S R Î± hÎ± âˆ˜â‚— U r S R Î² hÎ²) =
    U r S R (Î± * Î²) (hÎ±.mul hÎ²) := by
  ext a
  rw[U, U, U]
  simp only [MulEquiv.toMonoidHom_eq_coe, LinearMap.coe_comp, Function.comp_apply,
    MulMemClass.coe_mul, mul_inv_rev]
  apply (Subtype.coe_inj).mp
  conv_rhs =>
    apply AbstractHeckeOperator.HeckeOperator_apply
  conv_lhs =>
    apply AbstractHeckeOperator.HeckeOperator_apply
  conv_lhs =>
    arg 1; ext; arg 1; ext; arg 2;
    apply AbstractHeckeOperator.HeckeOperator_apply
  #check U_coset Î± hÎ±
  #check RestrictedProduct.mem_coset_and_mulSupport_subset_of_isProductAt

  sorry

lemma U_comm {v : HeightOneSpectrum (ğ“ F)}
    {Î± Î² : v.adicCompletionIntegers F} (hÎ± : Î± â‰  0) (hÎ² : Î² â‰  0) :
    U r S R Î± hÎ± âˆ˜â‚— U r S R Î² hÎ² =
    U r S R Î² hÎ² âˆ˜â‚— U r S R Î± hÎ± := by
  rw [U_mul, U_mul]
  congr 1
  rw [mul_comm]

end HeckeOperator

open HeckeOperator

/-- `HeckeAlgebra F D r S R` is the Hecke algebra associated to the weight 2
`R`-valued automorphic forms associated to the discriminant 1 totally definite
quaternion algebra `D` over the totally real field `F`, of level `Uâ‚(S)` where `S` is
a finite set of nonzero primes `v` of `ğ“ F`. To make sense of this definition we choose
a rigidification `r`, that is, a fixed `ğ”¸_F^âˆ`-linear
isomorphism `D âŠ—[F] ğ”¸_F^âˆ = Mâ‚‚(ğ”¸_F^âˆ)`, enabling us to define level structures and
Hecke operators `Táµ¥` and `Uáµ¥` using 2x2 matrices.

Details: `Uâ‚(S)` is the subgroup of `(D âŠ—[F] ğ”¸_F^âˆ)Ë£` associated, via `r`, to the
matrices which are in `GLâ‚‚(ğ“áµ¥)` for all `v âˆ‰ S` and which are of the form
`(a *; 0 a)` mod `v` for all `v âˆˆ S`. The Hecke algebra is defined to be the
sub-`R`-algebra of the weight 2 forms of level `Uâ‚(S)` generated by the following
two kinds of Hecke operators: first there are the operators
`Táµ¥` associated to the matrices `(Ï–áµ¥ 0; 0 1)` for `v âˆ‰ S` (here `Ï–áµ¥ âˆˆ ğ”¸_F^âˆ` is a local
uniformiser supported at `v`). Second, there are the Hecke operators `Uáµ¥,â‚`
for `v âˆˆ S` and `0 â‰  Î±áµ¥ âˆˆ ğ“áµ¥`, associated the matries `(Î±áµ¥ 0; 0 1)`.
These slightly nonstandard Hecke operators satisfy `Uáµ¥,â‚› * Uáµ¥,â‚œ = Uáµ¥,â‚›â‚œ`
and in particular this Hecke algebra is commutative (Hecke operators supported
at distinct primes commute because the decomposition of the double cosets
into single cosets can be done purely locally).
-/
def HeckeAlgebra : Type _ :=
  (Algebra.adjoin R ({T r R v | v âˆ‰ S} âˆª
  {Ï† | âˆƒ (v : HeightOneSpectrum (ğ“ F)) (_hv : v âˆˆ S)
         (Î± : v.adicCompletionIntegers F) (hÎ± : Î± â‰  0), Ï† = U r S R Î± hÎ±}) :
    Subalgebra R (WeightTwoAutomorphicFormOfLevel (U1 r S) R â†’â‚—[R]
      WeightTwoAutomorphicFormOfLevel (U1 r S) R))

namespace HeckeAlgebra

noncomputable instance instRing :
    Ring (HeckeAlgebra F D r S R) := inferInstanceAs <|
  Ring (Algebra.adjoin R _ : Subalgebra R (WeightTwoAutomorphicFormOfLevel (U1 r S) R â†’â‚—[R]
      WeightTwoAutomorphicFormOfLevel (U1 r S) R))

noncomputable instance instAlgebra :
    Algebra R (HeckeAlgebra F D r S R) := inferInstanceAs <|
  Algebra R (Algebra.adjoin R _ : Subalgebra R (WeightTwoAutomorphicFormOfLevel (U1 r S) R â†’â‚—[R]
      WeightTwoAutomorphicFormOfLevel (U1 r S) R))

noncomputable instance instCommRing :
    CommRing (HeckeAlgebra F D r S R) where
  __ := instRing F D r S R
  mul_comm := sorry -- #585 -- check on generators

variable {F S} in
/-- The Hecke operator Táµ¥ as an element of the Hecke algebra. -/
noncomputable def T (v : HeightOneSpectrum (ğ“ F)) (hv : v âˆ‰ S) : HeckeAlgebra F D r S R :=
  âŸ¨HeckeOperator.T r R v, by
    apply Algebra.subset_adjoin
    left
    use vâŸ©



variable {F S} in
/-- The Hecke operator Uáµ¥,â‚ as an element of the Hecke algebra. -/
noncomputable def U (v : HeightOneSpectrum (ğ“ F)) (hv : v âˆˆ S) (Î± : v.adicCompletionIntegers F)
    (hÎ± : Î± â‰  0) : HeckeAlgebra F D r S R :=
  âŸ¨HeckeOperator.U r S R Î± hÎ±, by
    apply Algebra.subset_adjoin
    right
    use v, hv, Î±, hÎ±âŸ©

end HeckeAlgebra

end TotallyDefiniteQuaternionAlgebra.WeightTwoAutomorphicForm
